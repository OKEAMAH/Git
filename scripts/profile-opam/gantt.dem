#
# Very simple Gantt Chart
# Demonstrate using timecolumn(N,format) to plot time data from 
# multiple columns
#

set datafile separator ","
$DATA << EOD
#Task start      end
dune build -p tezt -j 7,2023-06-06T16:41:29,2023-06-06T16:41:30
dune build -p tezt -j 7,2023-06-06T16:41:41,2023-06-06T16:42:00
EOD

set xdata time
timeformat = "%Y-%m-%dT%H:%M:%S"
# set format x "%b\n'%y"

set yrange [-1:]
# OneMonth = strptime("%m","2")
# set xtics OneMonth nomirror
OneMinute = strptime("%M","2")
set xtics OneMinute nomirror
set xtics scale 2, 0.5
set mxtics 4
set ytics nomirror
set grid x y
unset key
set title "{/=15 Simple Gantt Chart}\n\n{/:Bold Task start and end times in columns 2 and 3}"
set border 3

T(N) = timecolumn(N,timeformat)

set style arrow 1 filled size screen 0.02, 15 fixed lt 3 lw 1.5

plot $DATA using (T(2)) : ($0) : (T(3)-T(2)) : (0.0) : yticlabel(1) with vector as 1, \
     $DATA using (T(2)) : ($0) : 1 with labels right offset -2

