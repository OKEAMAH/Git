metavar termvar , x ::= 
  {{ isa string }} {{ coq nat }} {{ coq-equality }} {{ hol string }} {{ lex alphanum }}
  {{ tex \mathit{[[termvar]]} }} {{ com  term variable  }} 

grammar
  t :: 't_' ::=    {{ com term }}
    | x                   ::   :: Var   
    | \ x . t             ::   :: Lam  
    | t t'                ::   :: App  
    | ( t )               :: S :: paren 
    | { t / x } t'        :: M :: tsub  

  v :: 'v_' ::=    {{ com  value }}
    | \ x . t             ::   :: Lam   

  terminals :: 'terminals_' ::=
    | \                   ::   :: lambda  {{ tex \lambda }}
    | -->                 ::   :: red     {{ tex \longrightarrow }}


homs 't_'
  :: Lam  (+ bind x in t +)  

homs 't_'
  :: Var     {{ com variable }}         
  :: Lam     {{ com abstraction }}      
  :: App     {{ com application }}      
  :: paren   {{ ich [[t]] }} 
  :: tsub    {{ ich ( tsubst_t [[t]] [[x]] [[t']] ) }}

homs ''
  :: reduce  {{ com $[[t1]]$ reduces to $[[t2]]$ }} 


subrules
  v <:: t

substitutions
  single t x :: tsubst 

defns
  Jop :: '' ::=

    defn
    t1 --> t2 ::  :: reduce :: '' by 


    --------------------------  :: ax_app
    (\x.t12) v2 -->  {v2/x}t12

    t1 --> t1'
    -------------- :: ctx_app_fun
    t1 t --> t1' t

    t1 --> t1'
    -------------- :: ctx_app_arg
    v t1 --> v t1'

